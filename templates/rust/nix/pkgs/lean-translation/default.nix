{
  stdenvNoCC,
  writers,
  craneLib,
  # lake2nix,
  # Charon's Rust toolchain
  charonToolchain,
  aeneas,
  charon,
  aeneas-lean-backend,
  pkgs,
  src ? ../../..,
  lib,
  ...
}: let
  cleanedSrc = craneLib.cleanCargoSource src;

  cargoVendorDir = craneLib.vendorCargoDeps {
    src = cleanedSrc;
  };
  dependencies = [
    {
      name = "aeneas";
      path = aeneas-lean-backend;
    }
  ];

  lakefile = writers.writeTOML "lakefile.toml" {
    require = dependencies;
  };

  # Lean library path needed to compile generated .lean files to .olean.
  leanDepsClosure = pkgs.closureInfo {rootPaths = [aeneas-lean-backend];};
  leanClosurePaths =
    lib.filter (p: p != "")
    (lib.splitString "\n" (lib.removeSuffix "\n" (builtins.readFile "${leanDepsClosure}/store-paths")));
  leanLibDirs =
    lib.concatMap (p: [
      "${p}/lib/lean"
      "${p}/.lake/build/lib/lean"
    ])
    leanClosurePaths;
  leanLibPath = lib.concatStringsSep ":" leanLibDirs;

  translated = stdenvNoCC.mkDerivation {
    pname = "lean-translation";
    version = "0.1.0";
    src = cleanedSrc;

    nativeBuildInputs = [
      charonToolchain
      pkgs.lean4
    ];
    buildInputs = [
      charon
      aeneas
      aeneas-lean-backend
    ];

    dontConfigure = true;

    buildPhase = ''
      set -eux

      cp -r "$src" crate
      chmod -R u+w crate
      cd crate

      crate_manifest=$(find . -name Cargo.toml -print | head -n1 || true)

      if [ -z "$crate_manifest" ]; then
        echo "No Cargo.toml found under $PWD â€“ this source is not a Rust crate (or crate is in an unexpected place)." >&2
        exit 1
      fi

      crate_dir=$(dirname "$crate_manifest")
      cd "$crate_dir"

      echo "Using crate root: $PWD (found $crate_manifest)"

      mkdir -p .cargo
      cat ${cargoVendorDir}/config.toml > .cargo/config.toml
      cat >> .cargo/config.toml <<'EOF'
      [net]
      offline = true
      EOF

      export PATH=${charonToolchain}/bin:$PATH
      export RUSTUP_HOME=$PWD/.rustup
      export CARGO_HOME=$PWD/.cargo

      ${lib.getExe charon} cargo --preset=aeneas

      shopt -s nullglob
      llbc_files=(*.llbc)
      if [ "''${#llbc_files[@]}" -eq 0 ]; then
        echo "No .llbc files produced by charon; expected at least one." >&2
        exit 1
      fi

      lib_llbc_files=(lib*.llbc)
      if [ "''${#lib_llbc_files[@]}" -gt 0 ]; then
        llbc_files=("''${lib_llbc_files[@]}")
      fi

      for llbc_file in "''${llbc_files[@]}"; do
        echo "Translating $llbc_file to Lean"
        ${lib.getExe aeneas} -backend lean -split-files "$llbc_file"
      done
    '';

    installPhase =
      #sh
      ''
        set -eux

        lean_list=$(mktemp)
        find . -name '*.lean' ! -path '*/proofs/*' -print0 | sort -z > "$lean_list"

        if [ ! -s "$lean_list" ]; then
          echo "No Lean files generated by Aeneas" >&2
          echo "Expected at least one .lean translation" >&2
          exit 1
        fi

        mkdir -p "$out/lib/lean/Libtemplate"
        aggregator="$out/lib/lean/Libtemplate.lean"
        echo "-- Aggregates all generated Libtemplate modules" > "$aggregator"
        echo "-- This module was generated during Nix build" > "$aggregator"

        while IFS= read -r -d $'\0' f; do
          rel="''${f#./}"
          dest="$out/lib/lean/Libtemplate/$rel"
          install -Dm644 "$f" "$dest"

          module="''${rel%.lean}"
          module="''${module//\//.}"
          echo "import Libtemplate.''${module}" >> "$aggregator"
        done < "$lean_list"

        rm "$lean_list"

        install -Dm644 ${lakefile} $out/lib/lean/lakefile.toml
        install -Dm644 ${lakefile} $out/lakefile.toml

        # Precompile generated Lean sources to .olean in the store path.
        export LEAN_PATH="${leanLibPath}:$out/lib/lean"
        export LEAN_SRC_PATH="$LEAN_PATH"
        root="$out/lib/lean"

        # Build .olean/.ilean for generated modules. Compile Types first (Funs depends on it),
        # then the remaining modules, and finally the aggregator.
        modules=()
        if [ -f "$out/lib/lean/Libtemplate/Types.lean" ]; then
          modules+=("$out/lib/lean/Libtemplate/Types.lean")
        fi
        for src in "$out"/lib/lean/Libtemplate/*.lean; do
          if [ "$src" != "$out/lib/lean/Libtemplate/Types.lean" ]; then
            modules+=("$src")
          fi
        done
        modules+=("$aggregator")

        for src in "''${modules[@]}"; do
          base="''${src%.lean}"
          lean --root="$root" -o "$base.olean" "$src"
          lean --root="$root" -i "$base.ilean" "$src"
        done
      '';

    doCheck = false;
  };
in
  translated
